buildscript {
    repositories {
        mavenLocal()
        maven { url "https://repo.grails.org/grails/core" }
    }
    dependencies {
        classpath "org.grails:grails-gradle-plugin:$grailsVersion"
    }
}

plugins {
    id 'org.asciidoctor.convert' version '1.5.3'
}

version "1.0.0.BUILD-SNAPSHOT"
group "org.grails.plugins"

apply plugin:"eclipse"
apply plugin:"idea"
apply plugin:"org.grails.grails-plugin"
apply plugin:"org.grails.grails-plugin-publish"
apply plugin: 'codenarc'

repositories {
    mavenLocal()
    maven { url "https://repo.grails.org/grails/core" }
}

dependencies {
    compile "org.springframework.boot:spring-boot-starter-logging"
    compile "org.springframework.boot:spring-boot-autoconfigure"
    compile "org.grails:grails-core"
    console "org.grails:grails-console"
    profile "org.grails.profiles:plugin"
    provided "org.grails:grails-plugin-services"
    provided "org.grails:grails-plugin-domain-class"
    testCompile "org.grails:grails-gorm-testing-support"
    testCompile "org.grails:grails-plugin-testing"
}

bootRun {
    jvmArgs('-Dspring.output.ansi.enabled=always')
    addResources = true
}
// enable if you wish to package this plugin as a standalone application
bootRepackage.enabled = false
grailsPublish {
    userOrg = 'grails'
    githubSlug = 'grails-plugins/hibernate-logical-delete'
    license {
        name = 'Apache-2.0'
    }
    title = 'Grails Hibernate Logical Delete'
    desc = 'Adds logical delete capabilities to Grails 3 apps using Hibernate'
    developers = [jeffbrown: "Jeff Scott Brown"]
}


groovydoc.docTitle = "Grails Hibernate Logical Delete ${project.version}"
groovydoc.destinationDir = new File(buildDir, 'docs/api')

asciidoctor.outputDir = new File(buildDir, 'docs')
asciidoctor.separateOutputDirs = false

task docs(dependsOn:[groovydoc, asciidoctor])

codenarc {
    configFile file('config/codenarc/codenarc.groovy')
}
